#<AdxTL>@(#)0.0.0.0 $Revision$
Subprog GETDETALLEOF(PCOD_PLANTA, PCOD_ORDEN, PIDIOMA, ESTADO_ORDEN, FECHA_INICIO, FECHA_FIN, ESTADO_PRODUCCION, NUM_LINEA_PRODUCCION, COD_ARTICULO, DESCRIPCION_ARTICULO, CANTIDAD_TOTAL,
& CANTIDAD_PRODUCIDA, UNIDAD_LANZAMIENTO, ESTADO_MATERIAL, NUM_LINEA_MATERIAL, SECUENCIA_ESTRUCTURA, COD_MATERIAL, DESCRIPCION_MATERIAL, CANTIDAD_NECESARIA,
& CANTIDAD_CONSUMIDA, UNIDAD_CONSUMO, ESTADO_OPERACION,
& NUM_OPERACION,
& FECHA_INICIO_OPERACION,
& FECHA_FIN_OPERACION, COD_CENTRO_TRABAJO, DESCRIPCION_CENTRO_TRABAJO, COD_MANO_OBRA, DESCRIPCION_MANO_OBRA, NUM_MANO_OBRA, TIEMPO_PREVISTO, TIEMPO_REALIZADO, UNIDAD_TIEMPO)
  # Entrada
  Value Char PCOD_PLANTA
  Value Char PCOD_ORDEN
  Value Char PIDIOMA
  # Salida
  Variable Integer ESTADO_ORDEN
  Variable Char FECHA_INICIO
  Variable Char FECHA_FIN
  # Salida Alta Producción
  Variable Integer ESTADO_PRODUCCION()
  Variable Integer NUM_LINEA_PRODUCCION()
  Variable Char COD_ARTICULO()()
  Variable Char DESCRIPCION_ARTICULO()()
  Variable Decimal CANTIDAD_TOTAL()
  Variable Decimal CANTIDAD_PRODUCIDA()
  Variable Char UNIDAD_LANZAMIENTO()()
  # Salida Materiales
  Variable Integer ESTADO_MATERIAL()
  Variable Integer NUM_LINEA_MATERIAL()
  Variable Integer SECUENCIA_ESTRUCTURA()
  Variable Char COD_MATERIAL()()
  Variable Char DESCRIPCION_MATERIAL()()
  Variable Decimal CANTIDAD_NECESARIA()
  Variable Decimal CANTIDAD_CONSUMIDA()
  Variable Char UNIDAD_CONSUMO()()
  # Salida Operaciones
  Variable Integer ESTADO_OPERACION()
  Variable Integer NUM_OPERACION()
  Variable Char FECHA_INICIO_OPERACION()()
  Variable Char FECHA_FIN_OPERACION()()
  Variable Char COD_CENTRO_TRABAJO()()
  Variable Char DESCRIPCION_CENTRO_TRABAJO()()
  Variable Char COD_MANO_OBRA()()
  Variable Char DESCRIPCION_MANO_OBRA()()
  Variable Integer NUM_MANO_OBRA()
  Variable Decimal TIEMPO_PREVISTO()
  Variable Decimal TIEMPO_REALIZADO()
  Variable Integer UNIDAD_TIEMPO()

  Local Integer I : Raz I

  If !clalev([YMFGH]) : Local File MFGHEAD [YMFGH] : Endif
  If !clalev([YMFGI]) : Local File MFGITM [YMFGI] : Endif
  If !clalev([YMFGM]) : Local File MFGMAT [YMFGM] : Endif
  If !clalev([YMFGO]) : Local File MFGOPE [YMFGO] : Endif

  Read [YMFGH]MFG0 = PCOD_ORDEN
  If fstat = 0
    ESTADO_ORDEN = [YMFGH]MFGSTA
    FECHA_INICIO = num$([YMFGH]STRDAT)
    FECHA_FIN = num$([YMFGH]ENDDAT)

    # Información de los productos a fabricar
    Filter [YMFGI] Where MFGNUM = PCOD_ORDEN
    For [YMFGI]
      ESTADO_PRODUCCION(I) = [YMFGI]ITMSTA
      NUM_LINEA_PRODUCCION(I) = [YMFGI]MFGLIN
      COD_ARTICULO(I) = [YMFGI]ITMREF
      DESCRIPCION_ARTICULO(I) = func GETDESCRIPCIONARTICULO([YMFGI]ITMREF, PIDIOMA)
      CANTIDAD_TOTAL(I) = [YMFGI]EXTQTY
      CANTIDAD_PRODUCIDA(I) = [YMFGI]CPLQTY
      UNIDAD_LANZAMIENTO(I) = [YMFGI]UOM

      I += 1
    Next
    Filter [YMFGI]

    # Información de los materiales a consumir
    Raz I
    Filter [YMFGM] Where MFGNUM = PCOD_ORDEN
    For [YMFGM]
      ESTADO_MATERIAL(I) = [YMFGM]MATSTA
      NUM_LINEA_MATERIAL(I) = [YMFGM]MFGLIN
      SECUENCIA_ESTRUCTURA(I) = [YMFGM]BOMSEQ
      COD_MATERIAL(I) = [YMFGM]ITMREF
      DESCRIPCION_MATERIAL(I) = func GETDESCRIPCIONARTICULO([YMFGM]ITMREF, PIDIOMA)
      CANTIDAD_NECESARIA(I) = [YMFGM]RETQTY
      CANTIDAD_CONSUMIDA(I) = [YMFGM]USEQTY
      UNIDAD_CONSUMO(I) = [YMFGM]BOMUOM

      I += 1
    Next
    Filter [YMFGM]

    # Información de las operaciones
    Raz I
    Filter [YMFGO] Where MFGNUM = PCOD_ORDEN
    For [YMFGO]
      ESTADO_OPERACION(I) = [YMFGO]OPESTA
      NUM_OPERACION(I) = [YMFGO]OPENUM
      FECHA_INICIO_OPERACION(I) = num$([YMFGO]OPESTR)
      FECHA_FIN_OPERACION(I) = num$([YMFGO]OPEEND)
      COD_CENTRO_TRABAJO(I) = [YMFGO]EXTWST
      DESCRIPCION_CENTRO_TRABAJO(I) = func GETDESCRIPCIONCENTROTRABAJO(PCOD_PLANTA, [YMFGO]EXTWST, PIDIOMA)
      COD_MANO_OBRA(I) = [YMFGO]EXTLAB
      DESCRIPCION_MANO_OBRA(I) = func GETDESCRIPCIONCENTROTRABAJO(PCOD_PLANTA, [YMFGO]EXTLAB, PIDIOMA)
      NUM_MANO_OBRA(I) = [YMFGO]EXTLABNBR
      TIEMPO_PREVISTO(I) = [YMFGO]EXTOPETIM
      TIEMPO_REALIZADO(I) = [YMFGO]CPLOPETIM
      UNIDAD_TIEMPO(I) = [YMFGO]TIMUOMCOD

      I += 1
    Next
    Filter [YMFGO]

  Endif

  Close Local File [YMFGH]
  Close Local File [YMFGI]
  Close Local File [YMFGM]
  Close Local File [YMFGO]
End

Funprog GETTRADUCCION(PCODFIC, PZONE, PIDIOMA, PIDENT1, PIDENT2)
  Value Char PCODFIC
  Value Char PZONE
  Value Char PIDIOMA
  Value Char PIDENT1
  Value Char PIDENT2

  Local Char LRET(250) : Raz LRET

  If !clalev([YATE]) : Local File ATEXTRA [YATE] : Endif

  Read [YATE]AXX0 = PCODFIC;PZONE;PIDIOMA;PIDENT1;PIDENT2
  If fstat = 0
    LRET = [YATE]TEXTE
  Endif

  Close Local File [YATE]

End LRET

Funprog GETDESCRIPCIONARTICULO(PCOD_ARTICULO, PIDIOMA)
  Value Char PCOD_ARTICULO
  Value Char PIDIOMA

  Local Char DESCRIPCION(250) : Raz DESCRIPCION

  DESCRIPCION = func GETTRADUCCION("ITMMASTER", "DES1AXX", PIDIOMA, PCOD_ARTICULO, "")

  If DESCRIPCION = ""
    If !clalev([YITM]) : Local File ITMMASTER [YITM] : Endif

    Read [YITM]ITMREF = PCOD_ARTICULO
    If fstat = 0
      DESCRIPCION = [YITM]ITMDES1
    Endif

    Close Local File [YITM]
  Endif

End DESCRIPCION

Funprog GETDESCRIPCIONCENTROTRABAJO(PCOD_PLANTA, PCOD_CENTRO, PIDIOMA)
  Value Char PCOD_PLANTA
  Value Char PCOD_CENTRO
  Value Char PIDIOMA

  Local Char RES(250)

  RES = func GETTRADUCCION("WORKSTATIO", "WSTDESAXX", PIDIOMA, PCOD_CENTRO, PCOD_PLANTA)

  If RES = ""
    RES = "N/A"
  Endif
End RES
